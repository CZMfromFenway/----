
#设置玩家模式
execute if score rule.blockBreaking value matches 1 run gamemode survival @a[team=!]
execute if score rule.blockBreaking value matches 0 run gamemode adventure @a[team=!]

#防止玩家再次触发选卡准备完毕
scoreboard players reset @a ready

#设置游戏状态为开始
scoreboard players set game.ingame value 1

#公告
execute as @a run function missile_wars:text/settings_read_only
tellraw @a [{"text": "\n游戏开始！冲锋陷阵，直捣黄龙！","color": "blue"}]
tellraw @a [{"text": "强制停止游戏可使用","color": "gray"},"/",{"text":"trigger","color":"light_purple"},{"text":" stop","color":"aqua"}]
title @a title [{"text": "游 戏 开 始","color": "aqua"}]

#清空道具使用记录

scoreboard players reset @a[team=!] use.snowball
scoreboard players reset @a[team=!] use.arrow
scoreboard players reset @a[team=!] use.trident
scoreboard players reset @a[team=!] use.spectral_arrow
scoreboard players reset @a[team=!] use.ender_pearl
scoreboard players reset @a[team=!] use.blaze_spawn_egg
scoreboard players reset @a[team=!] use.guardian_spawn_egg
scoreboard players reset @a[team=!] use.creeper_spawn_egg
scoreboard players reset @a[team=!] use.ghast_spawn_egg
scoreboard players reset @a[team=!] use.witch_spawn_egg
scoreboard players reset @a[team=!] use.ocelot_spawn_egg
scoreboard players reset @a[team=!] use.wither_skeleton_spawn_egg
scoreboard players reset @a[team=!] use.iron_golem_spawn_egg
scoreboard players reset @a[team=!] use.spider_spawn_egg
scoreboard players reset @a[team=!] use.cave_spider_spawn_egg
scoreboard players reset @a[team=!] use.zombified_piglin_spawn_egg
scoreboard players reset @a[team=!] use.magma_cube_spawn_egg
scoreboard players reset @a[team=!] use.parrot_spawn_egg
scoreboard players reset @a[team=!] use.squid_spawn_egg

#分发初始卡组
clear @a[team=!]
xp set @a[team=!] 5 levels
item replace entity @a[team=!] hotbar.0 with bow[custom_name="{\"text\":\"弓\",\"color\":\"light_purple\",\"italic\":false}",lore=["{\"text\":\"请发射箭矢以摧毁敌方导弹\",\"color\":\"gray\",\"italic\":false}"],enchantments={flame:1,"missile_wars:explosive_arrow":1,"missile_royale:arrow_cost":1},unbreakable={show_in_tooltip:false}]
execute as @a[team=!] run function missile_royale:game/deal/deal
